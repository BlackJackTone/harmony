import lst1;

def lst_find(lst, v):
    let before = lst let after = (!before).next:
        lock(?(!before).lock);
        lock(?(!after).lock);
        while (!after).value < v:
            unlock(?(!before).lock);
            before = after;
            after = (!before).next;
            lock(?(!after).lock);
        ;
        result = (before, after);
    ;
;
def lst_insert(lst, v):
    let before, after = lst_find(lst, v):
        if (!after).value != v:
            (!before).next = lst_node(v, after);
        ;
        unlock(?(!before).lock);
        unlock(?(!after).lock);
    ;
;
def lst_remove(lst, v):
    let before, after = lst_find(lst, v):
        if (!after).value == v:
            (!before).next = (!after).next;
            unlock(?(!after).lock);
            free(after);
        else:
            unlock(?(!after).lock);
        ;
        unlock(?(!before).lock);
    ;
;
mylist = lst_node(-inf, lst_node(inf, None));
lst_insert(mylist, 3);
assert lst_contains(mylist, 3);
lst_remove(mylist, 3);
assert not lst_contains(mylist, 3);
