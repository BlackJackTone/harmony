import bag;

def tas(lk):
    atomic:
        result = ^lk;
        ^lk = True;
    ;
;
def Lock():
    result = False;
;
def lock(lk):
    while tas(lk):
        pass;
    ;
;
def unlock(lk):
    ^lk = False;
;
def Condition(lk):
    result = dict{ .lock: lk, .waiters: bagEmpty() };
;
def wait(c):
    let lk = (), blocked = True, cnt = 0:
        atomic:
            lk = (^c).lock;
            cnt = bagCount((^c).waiters, nametag());
            call bagAdd(&((^c).waiters), nametag());
            ^lk = False;
        ;
        while blocked:
            atomic:
                if (not (^lk)) and (bagCount((^c).waiters, nametag()) <= cnt):
                    ^lk = True;
                    blocked = False;
                ;
            ;
        ;
    ;
;
def notify(c):
    atomic:
        let waiters = (^c).waiters:
            if waiters != bagEmpty():
                call bagRemove(&((^c).waiters), bagChoose(waiters));
            ;
        ;
    ;
;
def notifyAll(c):
    (^c).waiters = bagEmpty();
;
def Semaphore(cnt):
    result = cnt;
;
def P(sema):
    let blocked = True:
        while blocked:
            atomic:
                if (^sema) > 0:
                    ^sema = (^sema) - 1;
                    blocked = False;
                ;
            ;
        ;
    ;
;
def V(sema):
    atomic:
        ^sema = (^sema) + 1;
    ;
;
