const N = 4;

def swap(p, q):
    let t = ^p:
        ^p = ^q; 
        ^q = t; 
    ;
;
def partition(lo, hi):
    let top = a[hi], i = lo - 1:
        for j in lo..(hi - 1):
            if a[j] <= top:
                i = i + 1;
                swap(&a[i], &a[j]);
            ;
        ;
        swap(&a[i + 1], &a[hi]); 
        result = i + 1;
    ;
;
def qsort():
    while todo != {}:
        let range = choose(todo):
            todo = todo - { range };
            let lo = range[0], hi = range[1], pivot = partition(lo, hi):
                if (pivot - 1) > lo:
                    assert (lo, pivot - 1) != range;
                    todo = todo + { (lo, pivot - 1) };
                ;
                if (pivot + 1) < hi :
                    assert (pivot + 1, hi) != range;
                    todo = todo + { (pivot + 1, hi) };
                ;
            ;
        ;
    ;
;
def sorted():
    result = True;
    for i in 1..(len(a) - 1):
        if a[i - 1] > a[i]:
            result = False;
        ;
    ;
;

const values = 1..N;
a = [ choose(values) for i in 1..choose(1..N) ];
todo = { (0, len(a) - 1) };
qsort();
assert sorted();
