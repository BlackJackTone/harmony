const MAX_ITEMS = 3;

def inc(pcnt):
    atomic:
        result = ^pcnt;
        ^pcnt = (^pcnt) + 1;
    ;
;
def swap(pv):
    atomic:
        result = ^pv;
        ^pv = ();
    ;
;
def produce(item):
    items[inc &(back)] = (item,);
;
def consume():
    result = ();
    while result == ():
        let range = back, i = 0:
            while (i < range) and (result == ()):
                result = swap(&(items[i]));
                i = i + 1;
            ;
        ;
    ;
    result = result[0];     # convert (item,) into item
;
back = 0;
items = [ () for i in 0..MAX_ITEMS ];

for i in 1..MAX_ITEMS:
    spawn produce(i);
;
for i in 1..choose(0..MAX_ITEMS):
    spawn consume();
;
