import synch;

def acquire_rlock():
    lock(&rlock);
    if nreaders == 0:
        lock(&rwlock);
    ;
    nreaders = nreaders + 1;
    unlock(&rlock);
;
def release_rlock():
    lock(&rlock);
    nreaders = nreaders - 1;
    if nreaders == 0:
        unlock(&rwlock);
    ;
    unlock(&rlock);
;
def acquire_wlock():
    lock(&rwlock);
;
def release_wlock():
    unlock(&rwlock);
;
rwlock = Lock();
rlock = Lock();
nreaders = 0;

def process():
    while choose({ False, True }):
        if choose({ .read, .write }) == .read:
            acquire_rlock();
            @rcs: assert atLabel.wcs == dict{};
            release_rlock();
        else:                       # .write
            acquire_wlock();
            @wcs: assert (atLabel.wcs == dict{ nametag(): 1 }) and
                         (atLabel.rcs == dict{})
                  ;
            release_wlock();
        ;
    ;
;
for i in 1..3:
    spawn process();
;
