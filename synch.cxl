def tas(self):
    atomic:
        result = ^self;
        ^self = True;
    ;
;
def Lock():
    result = False;
;
def lock(lk):
    while tas(lk):
        pass;
    ;
;
def unlock(lk):
    ^lk = False;
;
def Condition(lk):
    result = dict{ .lock: lk, .waiters: {} };
;
def wait(c):
    var lk = (^c).lock;
    var pid = getpid();
    (^c).waiters = (^c).waiters + { pid };
    ^lk = False;
    var blocked = True;
    while blocked:
        atomic:
            if not (^lk):
                if not(pid in (^c).waiters):
                    ^lk = True;
                    blocked = False;
                ;
            ;
        ;
    ;
;
def notify(c):
    if (^c).waiters != {}:
        var pid = choose((^c).waiters);
        (^c).waiters = (^c).waiters - { pid };
    ;
;
def notifyAll(c):
    (^c).waiters = {};
;
def Semaphore(cnt):
    result = cnt;
;
def P(sema):
    var blocked = True;
    while blocked:
        atomic:
            if (^sema) > 0:
                ^sema = (^sema) - 1;
                blocked = False;
            ;
        ;
    ;
;
def V(sema):
    atomic:
        ^sema = (^sema) + 1;
    ;
;
