import synch;

def process():
    while choose({ False, True }):
        if choose({ .read, .write }) == .read:
            let blocked = True:
                while blocked:
                    call lock &(rwlock);
                    if nwriters == 0:
                        nreaders = nreaders + 1;
                        blocked = False;
                    ;
                    call unlock &(rwlock);
                ;
            ;
            @rcs: assert atLabel.wcs == dict{}
                  ;
            call lock &(rwlock);
            nreaders = nreaders - 1;
            call unlock &(rwlock);
        else:                       # .write
            let blocked = True:
                while blocked:
                    call lock &(rwlock);
                    if (nreaders == 0) and (nwriters == 0):
                        nwriters = 1;
                        blocked = False;
                    ;
                    call unlock &(rwlock);
                ;
            ;
            @wcs: assert (atLabel.wcs == dict{ nametag(): 1 }) and
                         (atLabel.rcs == dict{})
                  ;
            call lock &(rwlock);
            nwriters = 0;
            call unlock &(rwlock);
        ;
    ;
;
rwlock = Lock();
nreaders = 0; nwriters = 0;
for i in 1..4:
    spawn process();
;
