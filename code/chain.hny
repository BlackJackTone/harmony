const NSERVERS = 3
const NOPS = 6

network = {}
final = None

def send(m):
    atomic: network |= { m }

def receive(predecessor, index):
    result = { x for (id, x) in network where (id == predecessor)
                        and ((x == .crash) or (x[0] == index)) }

def server(self, immortal):
    let history, predecessors = [], { 0 .. self - 1 }:
        while choose({ immortal, True }) and (len(history) < NOPS):
            if predecessors == {}:
                send(self, (len(history), self))
                history += [self,]
            else:
                select x in receive(max(predecessors), len(history)):
                    if x == .crash:
                        predecessors -= { max(predecessors) }
                    else:
                        send(self, x)
                        history += [x[1],]
        if len(history) == NOPS:
            atomic:
                assert (final == None) or (final == history)
                final = history
        else:
            send(self, .crash)

let survivor = choose({ 0 .. NSERVERS - 1 }):
    for i in { 0 .. NSERVERS - 1 }:
        spawn server(i, i == survivor)
