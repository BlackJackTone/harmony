const N = 4;

def swap(p, q):
    let t = ^p:
        ^p = ^q; 
        ^q = t; 
    ;
;
def partition(l, h):
    let x = a[h], i = l - 1:
        for j in l..(h - 1):
            if a[j] <= x:
                i = i + 1;
                swap(&a[i], &a[j]);
            ;
        ;
        swap(&a[i + 1], &a[h]); 
        result = i + 1;
    ;
;
def qsort():
    while todo != {}:
        let range = choose(todo):
            todo = todo - { range };
            let l = range[0], h = range[1], p = partition(l, h):
                if (p - 1) > l:
                    assert (l, p - 1) != range;
                    todo = todo + { (l, p - 1) };
                ;
                if (p + 1) < h :
                    assert (p + 1, h) != range;
                    todo = todo + { (p + 1, h) };
                ;
            ;
        ;
    ;
;
def sorted():
    result = True;
    for i in 1..(len(a) - 1):
        if a[i - 1] > a[i]:
            result = False;
        ;
    ;
;

const values = 1..N;
a = [ choose(values) for i in 1..choose(1..N) ];
todo = { (0, len(a) - 1) };
qsort();
assert sorted();
