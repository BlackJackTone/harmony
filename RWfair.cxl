import synch;

def V_one_r():
    if (nwritersEntered == 0) and (nwritersWaiting == 0) and (nreadersWaiting > 0):
        call V &(rsema);
    elif ((nreadersEntered + nwritersEntered) == 0) and (nwritersWaiting > 0):
        call V &(wsema);
    else:
        call V &(mutex);
    ;
;
def V_one_w():
    if ((nreadersEntered + nwritersEntered) == 0) and (nwritersWaiting > 0):
        call V &(wsema);
    elif (nwritersEntered == 0) and (nwritersWaiting == 0) and (nreadersWaiting > 0):
        call V &(rsema);
    else:
        call V &(mutex);
    ;
;
def acquire_rlock():
    call P &(mutex);
    if (nwritersEntered > 0) or (nwritersWaiting > 0):
        nreadersWaiting = nreadersWaiting + 1;
        call V &(mutex);
        call P &(rsema);
        nreadersWaiting = nreadersWaiting - 1;
    ;
    nreadersEntered = nreadersEntered + 1;
    call V_one_r();
;
def release_rlock():
    call P &(mutex);
    nreadersEntered = nreadersEntered - 1;
    call V_one_w();
;
def acquire_wlock():
    call P &(mutex);
    if (nreadersEntered + nwritersEntered) > 0:
        nwritersWaiting = nwritersWaiting + 1;
        call V &(mutex);
        call P &(wsema);
        nwritersWaiting = nwritersWaiting - 1;
    ;
    nwritersEntered = nwritersEntered + 1;
    call V_one_w();
;
def release_wlock():
    call P &(mutex);
    nwritersEntered = nwritersEntered - 1;
    call V_one_r();
;
mutex = Semaphore(1); rsema = Semaphore(0); wsema = Semaphore(0);
nreadersEntered = 0; nreadersWaiting = 0;
nwritersEntered = 0; nwritersWaiting = 0;

def process():
    while choose({ False, True }):
        if choose({ .read, .write }) == .read:
            call acquire_rlock();
            @rcs: assert atLabel.wcs == dict{};
            call release_rlock();
        else:                       # .write
            call acquire_wlock();
            @wcs: assert (atLabel.wcs == dict{ nametag(): 1 }) and
                         (atLabel.rcs == dict{})
                  ;
            call release_wlock();
        ;
    ;
;
rwlock = Lock();
rlock = Lock();
nreaders = 0;
for i in 1..4:
    spawn process();
;
