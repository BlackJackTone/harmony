const MAX_ITEMS = 3;

def inc(pcnt):
    atomic:
        result = !pcnt;
        !pcnt += 1;
    ;
;
def swap(pv):
    atomic:
        result = !pv;
        !pv = None;
    ;
;
def produce(item):
    items[inc(?back)] = (item,);
;
def consume():
    result = None;
    while result == None:
        let range, i = back, 0:
            while (i < range) and (result == None):
                result = swap(?items[i]);
                i += 1;
            ;
        ;
    ;
    result = result[0];     # convert (item,) into item
;
back = 0;
items = [ None for i in {0..MAX_ITEMS} ];

for i in {1..MAX_ITEMS}:
    spawn produce(i);
;
for i in {1..choose({0..MAX_ITEMS})}:
    spawn consume();
;
